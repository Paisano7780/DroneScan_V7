plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.dronescan.msdksample'
    compileSdk 34

    defaultConfig {
        applicationId "com.dronescan.msdksample"
        minSdk 26
        targetSdk 34
        versionCode 33
        versionName "2.13"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        ndk {
            abiFilters 'armeabi-v7a', 'x86', 'arm64-v8a'
        }
        
        multiDexEnabled true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    // ConfiguraciÃ³n para nombres de APK personalizados
    applicationVariants.all { variant ->
        variant.outputs.all {
            def versionName = variant.versionName
            def buildType = variant.buildType.name
            def fileName = "DroneScan_v${versionName}_${buildType}.apk"
            outputFileName = fileName
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    kotlinOptions {
        jvmTarget = '1.8'
    }
    
    buildFeatures {
        viewBinding true
    }
    
    packagingOptions {
        pickFirst '**/libc++_shared.so'
        pickFirst '**/libjsc.so'
        pickFirst '**/libc++_shared.so'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.7.22"
    
    // AndroidX
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    
    // RxJava dependencies (como Android-Bridge-App)
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    
    // DJI SDK dependencies (core only - excluimos dependencias pesadas innecesarias)
    implementation('com.dji:dji-sdk:4.16.4') {
        exclude group: 'com.dji', module: 'utmiss'
        exclude group: 'com.dji', module: 'fly-safe-database'
        exclude group: 'com.dji', module: 'library-anti-distortion'
    }
    compileOnly 'com.dji:dji-sdk-provided:4.16.4'
    
    // Barcode scanning
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
    implementation 'com.google.zxing:core:3.5.1'
    
    // CSV export
    implementation 'com.opencsv:opencsv:5.7.1'
    
    // USB Host y Storage Access
    implementation 'androidx.documentfile:documentfile:1.0.1'
    
    // File monitoring y observadores
    implementation 'androidx.work:work-runtime-ktx:2.8.1'
    
    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
